plugins {
    id 'dev.architectury.loom' version '1.10-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id "me.modmuss50.mod-publish-plugin" version "0.8.4"
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'
    apply plugin: 'me.modmuss50.mod-publish-plugin'

    base {
        archivesName = "${rootProject.archives_name}-${rootProject.minecraft_version}-${project.name}"
    }

    boolean isCommon = modLoader == "common"

    repositories {
        maven { url "https://maven.parchmentmc.org" }
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"

        /*mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.parchment_mapping_version}@zip")
        }*/

        mappings loom.officialMojangMappings()
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId "com.eirmax.elytraswapperplus"
                artifactId "elytraswapperplus-$modLoader-" + minecraft_version
                version mod_version
                from components.java
                pom {
                    name = "Elytra Swapper Plus " + modLoader.capitalize()
                    url = "https://github.com/eirmax/elytraswaperplus"
                }
            }
        }

        repositories {
            maven {
                url = "https://maven.exodusstudio.org/releases/"
                credentials {
                    username = System.getenv("EXODUS_MAVEN_USERNAME") // DO NOT CHANGE
                    password = System.getenv("EXODUS_MAVEN_KEY") // DO NOT CHANGE
                }
            }
        }
    }

    if (!isCommon) {
        publishMods {
            file = file("../${modLoader.capitalize()}/build/libs/elytraswapperplus-${minecraft_version}-${modLoader}-${mod_version}.jar")
            changelog = file("../changelog.md").getText('UTF-8')
            type = STABLE
            modLoaders.add(modLoader)
            displayName = "[${modLoader.capitalize()} ${minecraft_version}] Elytra Swapper Plus ${mod_version}"

            curseforge {
                projectId = "1236104"
                projectSlug = "elytraswaperplus" // Required for discord webhook
                accessToken = providers.environmentVariable("CURSEFORGE_API_KEY")
                minecraftVersions.add("${minecraft_version}")
                minecraftVersions.add("${supported_minecraft_version}")
                javaVersions.add(JavaVersion.VERSION_21)
            }
        }
    }
}
